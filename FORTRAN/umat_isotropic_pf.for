C     ABAQUS USER SUBROUTINE FOR PHASE FIELD FRACTURE IN ELASTO-PLASTIC SOLIDS

C     MATERIAL PROPERTIES TO BE GIVEN THROUGH THE INPUT FILE (*.INP) ARE
C     PROPS(1) = YOUNG'S MODULUS
C     PROPS(2) = POISSON RATIO
C     PROPS(3) = YIELD STRESS VS. EQUIVALENT PLASTIC STRAIN
C     PROPS(4) = YIELD STRESS VS. EQUIVALENT PLASTIC STRAIN
C     PROPS(5) = ...
C     PROPS(6) = ...
C     PROPS(7) = LC
C     PROPS(8) = GC

      SUBROUTINE UMAT(STRESS,STATEV,DDSDDE,SSE,SPD,SCD,
     1 RPL,DDSDDT,DRPLDE,DRPLDT,STRAN,DSTRAN,
     2 TIME,DTIME,TEMP,DTEMP,PREDEF,DPRED,MATERL,NDI,NSHR,NTENS,
     3 NSTATV,PROPS,NPROPS,COORDS,DROT,PNEWDT,CELENT,
     4 DFGRD0,DFGRD1,NOEL,NPT,KSLAY,KSPT,KSTEP,KINC)

      IMPLICIT DOUBLE PRECISION (A-H,O-Z) 
      PARAMETER (ZERO= 0.D0,ONE=1.0D0,TWO=2.0D0,THREE=3.0D0,SIX=6.0D0,
     1 NEWTON=10,TOLER=1.D-6)

      DIMENSION STRESS(NTENS),STRAN(NTENS),DSTRAN(NTENS),STRESS0(NTENS),
     1 DDSDDE(NTENS,NTENS),EELAS(NTENS),EPLAS(NTENS),PROPS(*),
     2 FLOW(NTENS),STATEV(NSTATV),TIME(2)

      REAL*8 EMOD,ENU,EBULK3,EBULK,EG,EG2,EG3,ELAM,XK

      EMOD=PROPS(1)
      ENU=PROPS(2)
      NVALUE=2
C
C     TOL FOR A-T2
C
      XK=0.0000001D0

      EBULK3=EMOD/(ONE-TWO*ENU)
      EG2=EMOD/(ONE+ENU)
      EG=EG2/TWO
      EG3=THREE*EG
      ELAM=(EBULK3-EG2)/THREE
C
C     ELASTIC STIFFNESS
C
      DO K1=1,NDI
        DO K2=1,NDI
          DDSDDE(K2,K1)=ELAM
        ENDDO
          DDSDDE(K1,K1)=EG2+ELAM
      ENDDO
      
      DO K1=NDI+1,NTENS
        DDSDDE(K1,K1)=EG
      ENDDO
C
C     RECOVER ELASTIC AND PLASTIC STRAINS 
C
      CALL ROTSIG(STATEV(1),DROT,EELAS,2,NDI,NSHR)
      CALL ROTSIG(STATEV(NTENS+1),DROT,EPLAS,2,NDI,NSHR)
C
C     RECOVER UNDEGRADED STRESS
C
      DO K1=1,NTENS
        STRESS0(K1)=STATEV(K1+2*NTENS)
      ENDDO
C
C     EQUIVALENT PLASTIC STRAIN
C
      EQPLAS=STATEV(1+3*NTENS)
C
C     SPECIFIC ELASTIC STRAIN ENERGY
C
      SSE=STATEV(2+3*NTENS)
C
C     SPECIFIC PLASTIC DISSIPATION
C
      SPD=STATEV(3+3*NTENS)
C
C     CALCULATE PREDICTOR STRESS AND ELASTIC STRAIN
C
      DO K1=1,NTENS
        EELAS(K1)=EELAS(K1)+DSTRAN(K1)
        DO K2=1,NTENS
          STRESS0(K2)=STRESS0(K2)+DDSDDE(K2,K1)*DSTRAN(K1)
        ENDDO
      ENDDO

      IF ((NPROPS.GT.2).AND.(PROPS(3).GT.ZERO)) THEN
C
C     CALCULATE MISES STRESS
C
        SMISES=(STRESS0(1)-STRESS0(2))**2+(STRESS0(2)-STRESS0(3))**2
     1  +(STRESS0(3)-STRESS0(1))**2
        DO K1=NDI+1,NTENS
          SMISES=SMISES+SIX*STRESS0(K1)**2
        ENDDO 
        SMISES=SQRT(SMISES/TWO)

        CALL HARDSUB(SYIEL0,HARD,EQPLAS,PROPS(3),NVALUE)
C
C     DETERMINE IF ACTIVELY YIELDING
C
        IF (SMISES.GT.(ONE+TOLER)*SYIEL0) THEN
          SHYDRO=(STRESS0(1)+STRESS0(2)+STRESS0(3))/THREE
          DO K1=1,NDI
            FLOW(K1)=(STRESS0(K1)-SHYDRO)/SMISES
          ENDDO
          DO K1=NDI+1,NTENS
            FLOW(K1)=STRESS0(K1)/SMISES
          ENDDO
C
C     SOLVE FOR EQUIV STRESS, NEWTON ITERATION
C
          SYIELD=SYIEL0
          DEQPL=ZERO
          DO KEWTON=1,NEWTON
            RHS=SMISES-EG3*DEQPL-SYIELD
            DEQPL=DEQPL+RHS/(EG3+HARD)
            CALL HARDSUB(SYIELD,HARD,EQPLAS+DEQPL,PROPS(3),NVALUE)
            IF(ABS(RHS).LT.TOLER*SYIEL0) GOTO 100
          ENDDO
          WRITE(6,*) 'PLASTICITY ALGORITHM DID NOT CONVERGE'
 100      CONTINUE
C
C     CALC STRESS AND UPDATE STRAINS
C
          DO K1=1,NDI
            STRESS0(K1)=FLOW(K1)*SYIELD+SHYDRO
            EPLAS(K1)=EPLAS(K1)+THREE*FLOW(K1)*DEQPL/TWO
            EELAS(K1)=EELAS(K1)-THREE*FLOW(K1)*DEQPL/TWO
          ENDDO
          DO K1=NDI+1,NTENS
            STRESS0(K1)=FLOW(K1)*SYIELD
            EPLAS(K1)=EPLAS(K1)+THREE*FLOW(K1)*DEQPL
            EELAS(K1)=EELAS(K1)-THREE*FLOW(K1)*DEQPL
          ENDDO
          EQPLAS=EQPLAS+DEQPL
C
C     UPDATE SPECIFIC PLASTIC DISSIPATION
C
          SPD=SPD+DEQPL*(SYIEL0+SYIELD)/TWO
C
C     MATERIAL JACOBIAN
C
          EFFG=EG*SYIELD/SMISES
          EFFG2=TWO*EFFG
          EFFG3=THREE*EFFG2/TWO
          EFFLAM=(EBULK3-EFFG2)/THREE
          EFFHRD=EG3*HARD/(EG3+HARD)-EFFG3

          DO K1=1,NDI
            DO K2=1,NDI
              DDSDDE(K2,K1)=EFFLAM
            ENDDO
            DDSDDE(K1,K1)=EFFG2+EFFLAM
          ENDDO
          DO K1=NDI+1,NTENS
            DDSDDE(K1,K1)=EFFG
          ENDDO
          DO K1=1,NTENS
            DO K2=1,NTENS
              DDSDDE(K2,K1)=DDSDDE(K2,K1)+FLOW(K2)*FLOW(K1)*EFFHRD
            ENDDO
          ENDDO

        ENDIF

      ENDIF
C
C     PHASE FIELD LENGTH SCALE
C
      XL=PROPS(7)
C
C     FRACTURE TOUGHNESS
C
      GC=PROPS(8)
C
C     FRACTURE PHASE FIELD VARIABLE
C
      PHI=MAX(TEMP+DTEMP,STATEV(31))
      PHI=MIN(PHI,ONE)
      HIST=STATEV(32)
C      
C     DEGRADATION FUNCTION
C
      DEGFNC=ZERO
      DEGFNC=(ONE-PHI)**2+XK
      STRESS=STRESS0*DEGFNC
      DDSDDE=DDSDDE*DEGFNC

      SSE=ZERO
      DO K1=1,NTENS
        SSE=SSE+STRESS0(K1)*EELAS(K1)/TWO
      ENDDO 
      
      PSI=SSE+SPD
      H=MAX(HIST,PSI)
      RPL=-(PHI/XL**2-2.D0*(1.D0-PHI)*H/(GC*XL))
      DRPLDT=-(1.D0/XL**2+2.D0*H/(GC*XL))
C
C     STORE STATE VARIABLES
C
      DO K1=1,NTENS
        STATEV(K1)=EELAS(K1)
        STATEV(K1+NTENS)=EPLAS(K1)
        STATEV(K1+2*NTENS)=STRESS0(K1)
      ENDDO

      STATEV(1+3*NTENS)=EQPLAS
      STATEV(2+3*NTENS)=SSE
      STATEV(3+3*NTENS)=SPD
      STATEV(4+3*NTENS)=DEQPL
      STATEV(5+3*NTENS)=SYIELD
      STATEV(6+3*NTENS)=SYIEL0
      STATEV(25)=SSE
      STATEV(26)=SPD
      STATEV(27)=DEQPL
      STATEV(28)=SYIELD
      STATEV(29)=SYIEL0
      STATEV(30)=STRESS0(2)
      STATEV(31)=PHI
      STATEV(32)=H
      
C       IF ((NOEL.EQ.27587).AND.(NPT.EQ.1)) THEN
C         WRITE(*,*) TIME(2),STRESS,TEMP
C       ENDIF
      RETURN
      END

C     ==================================================================      
C     HARDENING FUNCTION  
C     ==================================================================
      SUBROUTINE HARDSUB(SYIELD,HARD,EQPLAS,TABLE,NVALUE)

      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      PARAMETER (ZERO=0.D0)
      DIMENSION TABLE(2,NVALUE)
C
C     SET YIELD STRESS TO LAST VALUE OF TABLE, HARDENING TO ZERO
C
      SYIELD=TABLE(1,NVALUE)
      HARD=ZERO
C
C     IF MORE THAN ONE ENTRY, SEARCH TABLE
C
      IF(NVALUE.GT.1) THEN
        DO K1=1,NVALUE-1
          EQPL1=TABLE(2,K1+1)
          IF(EQPLAS.LT.EQPL1) THEN
            EQPL0=TABLE(2,K1)
            IF(EQPL1.LE.EQPL0) THEN
              WRITE(6,200)
200           FORMAT(//,30X,'***ERROR - PLASTIC STRAIN MUST BE ',
     1                 'ENTERED IN ASCENDING ORDER')
              CALL XIT
            ENDIF
C
C     CURRENT YIELD STRESS AND HARDENING
C
            DEQPL=EQPL1-EQPL0
            SYIEL0=TABLE(1,K1)
            SYIEL1=TABLE(1,K1+1)
            DSYIEL=SYIEL1-SYIEL0
            HARD=DSYIEL/DEQPL
            SYIELD=SYIEL0+(EQPLAS-EQPL0)*HARD
            GOTO 300
          ENDIF
        ENDDO
300     CONTINUE
      ENDIF
      RETURN
      END